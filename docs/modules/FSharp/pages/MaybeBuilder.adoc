= Maybe Builder
:title: Maybe Builder
:navtitle: Maybe Builder
:source-highlighter: highlight.js
:highlightjs-languages: fsharp



MaybeBuilder is useful if you have a chain of if/else statements.


[source,fsharp]
----
let divideBy bottom top =
    if bottom = 0
    then None
    else Some (top/bottom)
----

Here is a workflow that attempts to divide a starting number three times.


[source,fsharp]
----
let divideByWorkflow init x y z =
    let a = init |> divideBy x
    match a with
    | None -> None  //give up
    | Some a' ->    //keep going
        let b = a' |> divideBy y
        match b with
        | None -> None //give up
        | Some b' ->   //keep going
            let c = b' |> divideBy z
            match c with
            | None -> None
            | Some c' ->
                Some c'

let good = divideByWorkflow 12 3 2 1
let bad = divideByWorkflow 12 3 0 1
----

Once more we define a new type(MaybeBuilder) and make an instance of the type (maybe)

[source,fsharp]
----
type MaybeBuilder() =

    member this.Bind(x,f) =
        match x with
        | None -> None
        | Some a -> f a

    member this.Return(x) =
        Some x

let maybe = new MaybeBuilder()

let divideByWorkflowM init x y z =
    maybe
        {
            let! a = init |> divideBy x
            let! b = a |> divideBy y
            let! c = b |> divideBy z
            return c
        }

let good1 = divideByWorkflowM 12 3 2 1
----

F# converts the above computation expression into something like this:

[source,fsharp]
----
let good1 = divideByWorkflowM 12 3 2 1
let good2 = maybe.Bind(divideBy 3 12, fun a ->
                                maybe.Bind(divideBy 2 a, fun b ->
                                    maybe.Bind(divideBy 1 b, fun c ->
                                        Some c
                                        )))
let bad1 = divideByWorkflowM 12 3 0 1
let bad2 = maybe.Bind(divideBy 3 12, fun a ->
                                maybe.Bind(divideBy 0 a, fun b ->
                                    maybe.Bind(divideBy 1 b, fun c ->
                                        Some c
                                        )))

----